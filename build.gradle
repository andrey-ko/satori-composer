gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS

apply plugin: 'idea'

group 'com.satori'
version '0.2.8-SNAPSHOT'

idea {
  project {
    jdkName = '1.8'
    languageLevel = '1.8'
  }
  module {
    inheritOutputDirs = false
    outputDir = file(".out/idea")
    testOutputDir = file(".out/idea-tests")
  }
}

task generateReadme(type: TemplateTask) {
  group 'codegen'
  template = file('readme.template.md')
  output = file('readme.md')
  outputs.upToDateWhen { false }
}

subprojects {
  group rootProject.group
  version rootProject.version

  apply plugin: 'java'
  apply plugin: 'idea'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  //tasks.withType(Javadoc).all { enabled = false }

  buildDir = '.out'
  sourceSets.main.java.srcDirs = ['src']
  sourceSets.main.resources.srcDirs = ['res']
  sourceSets.test.java.srcDirs = ['tests/src']
  sourceSets.test.resources.srcDirs = ['tests/res']

  compileJava.options.compilerArgs << "-Xlint:unchecked"
  compileJava.options.compilerArgs << "-Xlint:deprecation"

  idea {
    module {
      inheritOutputDirs = false
      outputDir = file(".out/idea")
      testOutputDir = file(".out/idea-tests")
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
  }
}

configure([project(':composer'), project(':mods'), project(':mods-suite')]) {
  apply from: rootProject.file('publishing.gradle')
}
